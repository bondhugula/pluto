dnl
dnl   /**------- <| --------------------------------------------------------**
dnl    **         A                     Clan                                **
dnl    **---     /.\   -----------------------------------------------------**
dnl    **   <|  [""M#               configure.in                            **
dnl    **-   A   | #   -----------------------------------------------------**
dnl    **   /.\ [""M#         First version: 30/04/2008                     **
dnl    **- [""M# | #  U"U#U  -----------------------------------------------**
dnl         | #  | #  \ .:/
dnl         | #  | #___| #
dnl ******  | "--'     .-"  ***************************************************
dnl *     |"-"-"-"-"-#-#-##   Clan : the Chunky Loop Analyser (experimental)  *
dnl ****  |     # ## ######  **************************************************
dnl *      \       .::::'/                                                    *
dnl *       \      ::::'/     Copyright (C) 2008 Cedric Bastoul               *
dnl *     :8a|    # # ##                                                      *
dnl *     ::88a      ###      This is free software; you can redistribute it  *
dnl *    ::::888a  8a ##::.   and/or modify it under the terms of the GNU     *
dnl *  ::::::::888a88a[]:::   Lesser General Public License as published by   *
dnl *::8:::::::::SUNDOGa8a::. the Free Software Foundation, either version 3  *
dnl *::::::::8::::888:Y8888::                of the License, or (at your      *
dnl *::::':::88::::888::Y88a::::::::::::...  option) any later version.       *
dnl *::'::..    .   .....   ..   ...  .                                       *
dnl * This software is distributed in the hope that it will be useful, but    *
dnl * WITHOUT ANY WARRANTY; without even the implied warranty of              *
dnl * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU       *
dnl * General Public License  for more details.	                              *
dnl *                                                                         *
dnl * You should have received a copy of the GNU Lesser General Public        *
dnl * License along with software; if not, write to the Free Software         *
dnl * Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307    *
dnl * USA                                                                     *
dnl *                                                                         *
dnl * Clan, the Chunky Loop Analyser                                          *
dnl * Written by Cedric Bastoul, Cedric.Bastoul@inria.fr                      *
dnl *                                                                         *
dnl ***************************************************************************/
dnl
dnl Input file for autoconf to build a configuration shellscript.


AC_PREREQ(2.13)
dnl Fill here the @bug email adress.
AC_INIT([clan], [0.6.0], [cedric.bastoul@inria.fr,pouchet@cse.ohio-state.edu])
dnl A common file, which serve as a test.
AC_CONFIG_SRCDIR([include/clan/symbol.h])
dnl Put as most as possible configuration files to an auxialiry
dnl directory.
AC_CONFIG_AUX_DIR([autoconf])
dnl Initialize automake. Here, a special tar version that enables
dnl (very) long filenames.
AM_INIT_AUTOMAKE([1.9 tar-ustar no-define foreign dist-bzip2])

dnl default version
BITS="64"
CLAN_INT_T=CLAN_INT_T_IS_LONGLONG


dnl /**************************************************************************
dnl  *                              Checking                                  *
dnl  **************************************************************************/


dnl Checks for programs.
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_CHECK_PROG(CD, cd)
AC_PROG_LIBTOOL
AC_PROG_LEX
AC_PROG_YACC
AC_CHECK_PROGS(DOXYGEN,doxygen,doxygen)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([errno.h stddef.h stdlib.h string.h strings.h unistd.h])

dnl Checks for library functions.
AC_CHECK_FUNCS(strtol)


dnl /**************************************************************************
dnl  *                             Option setting                             *
dnl  **************************************************************************/

AC_ARG_ENABLE(internal-scoplib,
	AC_HELP_STRING([--enable-internal-scoplib],
              	           [Use shipped ScopLib]),
	[ if test "x$enableval" = xno; then 
	    shipped_scoplib=no;
          else
	    shipped_scoplib=yes;
          fi
	], [ shipped_scoplib=uncertain ])


dnl Offer --with-scoplib.
AC_ARG_WITH(scoplib,
	    AC_HELP_STRING([--with-scoplib=DIR],
              	           [DIR Location of SCOPLIB package]),
            [if test "x$shipped_scoplib" != xyes; then 
	        with_scoplib=$withval;
	        CPPFLAGS="${CPPFLAGS} -I$withval/include";
	        LDFLAGS="${LDFLAGS} -L$withval/lib";
		shipped_scoplib=no;
	     fi;
	    ],
            [if test "x$shipped_scoplib" = xno; then 
	    	with_scoplib=yes;
	     else
	     	with_scoplib=check;
	     fi;
	     ])
dnl Check for scoplib existence.
AS_IF([test "x$with_scoplib" != xno ],
      [AC_CHECK_LIB([scoplib], [scoplib_scop_read],
      [LIBS="-lscoplib $LIBS";
       AC_DEFINE([HAVE_LIBSCOPLIB], [1], [Define if you have libscoplib])
      ],
      [if test "x$with_scoplib" != xcheck; then
       AC_MSG_FAILURE([Test for SCOPLIB failed. Use --with-scoplib to specify libscoplib path.]);
       fi;
      ])
])

AS_IF([test "x$shipped_scoplib" != xno ], 
      [abs_top_srcdir=`cd $srcdir; pwd`;
       abs_top_builddir=`pwd`;
       test -f $abs_top_srcdir/scoplib/scoplib-0.2.0
       if [[ ! $? ]] ; then
       	  cd $abs_top_srcdir/scoplib && tar xzf scoplib-0.2.0.tar.gz ; cd -;
       fi;
       AC_CONFIG_SUBDIRS([scoplib/scoplib-0.2.0])
       BUILD_SCOPLIB="scoplib/scoplib-0.2.0";
       CPPFLAGS="${CPPFLAGS} -I$abs_top_srcdir/scoplib/scoplib-0.2.0/include -I$abs_top_builddir/scoplib/scoplib-0.2.0/include";
       LDFLAGS="${LDFLAGS} -L$abs_top_builddir/scoplib/scoplib-0.2.0/source";
       LIBS="-lscoplib $LIBS";
      ],
      [BUILD_SCOPLIB=""])


dnl /**************************************************************************
dnl  *                            Substitutions                               *
dnl  **************************************************************************/


dnl Substitutions to do.
AC_SUBST(ac_aux_dir)
AC_SUBST(abs_top_srcdir)
AC_SUBST(BUILD_SCOPLIB)

dnl Configure Makefiles.
AC_CONFIG_FILES([
	Makefile
	doc/Makefile
	doc/Doxyfile
	include/Makefile
	include/clan/clan.h
	source/Makefile
	tests/Makefile
	],
	[test -z "$CONFIG_HEADERS" || echo timestamp > source/stamp-h.in])

AC_OUTPUT


echo "             /*-----------------------------------------------*"
echo "              *           Clan configuration is OK            *"
echo "              *-----------------------------------------------*/"
echo "It appears that your system is OK to start Clan compilation. You need"
echo "now to type \"make\". Lastly type \"make install\" to install Clan on"
echo "your system (log as root if necessary)."
